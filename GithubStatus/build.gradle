buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

def majorVersion = 1;
def minorVersion = 1;
def buildVersion = 0;


android {
    compileSdkVersion 21
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21

        versionName "${majorVersion}.${minorVersion}" + (buildVersion == 0 ? "" : "${buildVersion}")
        versionCode ((majorVersion * 100) + (minorVersion * 10) + (buildVersion * 1))
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile new File("notYourRealFileName")
            keyAlias "notYourRealKeystoreAlias"
            storePassword "notYourRealPassword"
            keyPassword "notYourRealPassword"
        }
    }

    buildTypes {

        all {
            buildConfigField "String", "SENDER_ID", "\"196611706338\""
        }

        debug {
            def proc = (project.projectDir.absolutePath + "/dev.sh").execute()
            proc.waitFor()
            def localhostIpAddr = "http://" + proc.in.text.split("\n")[0] + ":8080"

            buildConfigField "String", "SERVER_URL", "\"${localhostIpAddr}\""
        }

        release {
            buildConfigField "String", "SERVER_URL", "\"http://githubstatus.appspot.com\""

            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

task askForPasswords << {

    if (System.console() == null) {
        throw new RuntimeException("No console available!")
    }

    def filePath    = new File(System.getenv("KEYSTORE_PATH"))
    def alias       = System.getenv("KEYSTORE_ALIAS")
    def storePw     = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw       = new String(System.console().readPassword("Key password for '" + alias + "': "))

    android.signingConfigs.release.storeFile = filePath
    android.signingConfigs.release.keyAlias = alias
    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if ("packageRelease".equals(theTask.name)) {
        theTask.dependsOn "askForPasswords"
    }
}

dependencies {
    compile 'com.google.android.gms:play-services:3.1.+'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.squareup.okhttp:okhttp:1.5.3'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.makeramen:roundedimageview:1.3.0'
}
