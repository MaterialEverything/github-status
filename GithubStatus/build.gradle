buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 18
    }

    signingConfigs {
        release {
            storeFile new File("notYourRealFileName")
            keyAlias "notYourRealKeystoreAlias"
            storePassword "notYourRealPassword"
            keyPassword "notYourRealPassword"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

task askForPasswords << {

    if (System.console() == null) {
        throw new UnsupportedOperationException("No console available!")
    }

    def filePath = new File(System.getenv("KEYSTORE_PATH"))
    def alias = System.getenv("KEYSTORE_ALIAS")
    def storePw = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw  = new String(System.console().readPassword("Key password for '" + alias + "': "))

    android.signingConfigs.release.storeFile = filePath
    android.signingConfigs.release.keyAlias = alias
    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

dependencies {
	compile 'com.android.support:support-v4:18.0.0'
    compile 'com.j256.ormlite:ormlite-android:4.45'
    compile 'com.squareup.okhttp:okhttp:1.5.3'
    compile 'com.google.code.gson:gson:2.2.4'
}
